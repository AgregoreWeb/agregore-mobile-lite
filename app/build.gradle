plugins {
  id "com.android.application"
}

android {
  namespace "to.holepunch.bare.android"
  compileSdk 34
  ndkVersion "27.2.12479018"

  defaultConfig {
    applicationId "to.holepunch.bare.android"
    minSdk 28
    targetSdk 34
    versionCode 1
    versionName "1.0"
  }

  sourceSets {
    main {
      jniLibs.srcDirs "src/main/addons", "libs/bare-kit/jni"
    }
  }
}

task link(type: Exec) {
  workingDir ".."

  commandLine([
    "npx", "bare-link",
    "--target", "android-arm",
    "--target", "android-arm64",
    "--target", "android-ia32",
    "--target", "android-x64",
    "--needs", "libbare-kit.so",
    "--out", "app/src/main/addons"
  ])
}

task pack(type: Exec) {
  workingDir ".."

  commandLine([
    "npx", "bare-pack",
    "--platform", "android",
    "--linked",
    "--out", "app/src/main/assets/app.bundle",
    "app/src/main/js/app.js"
  ])
}

preBuild.dependsOn link, pack

dependencies {
  api fileTree(dir: "libs", include: ["bare-kit/classes.jar"])
}
